mutable struct LevelSetEquation
    terms::Tuple
    integrator::TimeIntegrator
    state::MeshField
    t::Float64
    buffers
end

"""
    LevelSetEquation(; terms, levelset, boundary_conditions, t = 0, integrator = RK3())

Create a of a level-set equation of the form `Ï•â‚œ + sum(terms) = 0`, where each `t âˆˆ terms`
is a [`LevelSetTerm`](@ref) and `levelset` is the initial [`LevelSet`](@ref).

Calling [`integrate!(ls, tf)`](@ref) will evolve the level-set equation up to time `tf`,
modifying the `current_state(eq)` and `current_time(eq)` of the object `eq` in the process
(and therefore the original `levelset`).

Boundary conditions can be specified in two ways. If a single `BoundaryCondition` is
provided, it will be applied uniformly to all boundaries of the domain. To apply different
boundary conditions to each boundary, pass a tuple of the form `(bc_x, bc_y, ...)` with as
many elements as dimensions in the domain. If `bc_x` is a `BoundaryCondition`, it will be
applied to both boundaries in the `x` direction. If `bc_x` is a tuple of two
`BoundaryCondition`s, the first will be applied to the left boundary and the second to the
right boundary. The same logic applies to the other dimensions.

The optional parameter `t` specifies the initial time of the simulation, and `integrator` is
the [`TimeIntegrator`](@ref) used to evolve the level-set equation.

```jldoctest; output = true
using LevelSetMethods, StaticArrays
grid = CartesianGrid((-1, -1), (1, 1), (50, 50))    # define the grid
Ï• = LevelSet(x -> x[1]^2 + x[2]^2 - 0.5^2, grid)    # initial shape
ð® = MeshField(x -> SVector(1, 0), grid)             # advection velocity
terms = (AdvectionTerm(ð®),)            # advection and curvature terms
bc = PeriodicBC()                                   # periodic boundary conditions
eq = LevelSetEquation(; terms, levelset = Ï•, bc)    # level-set equation

# output

Level-set equation given by

 	 Ï•â‚œ + ð® â‹… âˆ‡ Ï• = 0

Current time 0.0

```
"""
function LevelSetEquation(; terms, integrator = RK3(), levelset, t = 0, bc)
    N = dimension(levelset)
    terms = _normalize_terms(terms, N)
    bc = _normalize_bc(bc, N)
    # append boundary conditions to the state
    state = add_boundary_conditions(levelset, bc)
    # create buffers for the time-integrator
    nb = number_of_buffers(integrator)
    buffers = ntuple(_ -> deepcopy(state), nb)
    return LevelSetEquation(terms, integrator, state, t, buffers)
end

function _normalize_terms(terms, dim)
    if isa(terms, LevelSetTerm) # single term
        return (terms,)
    else
        N = length(terms)
        return ntuple(N) do i
            if isa(terms[i], LevelSetTerm)
                return terms[i]
            else
                throw(ArgumentError("invalid term $(terms[i]) on entry $i"))
            end
        end
    end
end

function _normalize_bc(bc, dim)
    if isa(bc, BoundaryCondition)
        return ntuple(_ -> (bc, bc), dim)
    else
        length(bc) == dim || throw(ArgumentError("invalid number of boundary conditions"))
        return ntuple(dim) do i
            if isa(bc[i], BoundaryCondition)
                return (bc[i], bc[i])
            else
                length(bc[i]) == 2 && all(isa(bc[i][n], BoundaryCondition) for n in 1:2) ||
                    throw(ArgumentError("invalid boundary condition for dimension $i"))
                # check that periodic boundary conditions are not mixed with others
                if any(isa(bc[i][n], PeriodicBC) for n in 1:2)
                    all(isa(bc[i][n], PeriodicBC) for n in 1:2) || throw(
                        ArgumentError(
                            "periodic boundary conditions cannot be mixed with others in dimension $i",
                        ),
                    )
                end
                return (bc[i][1], bc[i][2])
            end
        end
    end
end

function Base.show(io::IO, eq::LevelSetEquation)
    print(io, "Level-set equation given by\n")
    print(io, "\n \t Ï•â‚œ")
    terms = eq.terms
    for term in terms
        print(io, " + ")
        print(io, term)
    end
    print(io, " = 0")
    print(io, "\n\nCurrent time $(eq.t)")
    return io
end

# getters
current_state(ls::LevelSetEquation) = ls.state
current_time(ls::LevelSetEquation) = ls.t[]
buffers(ls::LevelSetEquation) = ls.buffers
time_integrator(ls) = ls.integrator
terms(ls) = ls.terms
boundary_conditions(ls) = boundary_conditions(ls.state)
mesh(ls::LevelSetEquation) = mesh(ls.state)

"""
    integrate!(ls::LevelSetEquation,tf,Î”t=Inf)

Integrate the [`LevelSetEquation`](@ref) `ls` up to time `tf`,
mutating the `levelset` and `current_time` of the object `ls` in the
process.

An optional parameter `Î”t` can be passed to specify a maximum time-step
allowed for the integration. Note that the internal time-steps taken to evolve
the level-set up to `tf` may be smaller than `Î”t` due to stability reasons
related to the `terms` and `integrator` employed.
"""
function integrate!(ls::LevelSetEquation, tf, Î”t = Inf)
    tc = current_time(ls)
    msg = "final time $(tf) must be larger than the initial time $(tc):
           the level-set equation cannot be solved back in time"
    @assert tf >= tc msg
    # append boundary conditions for integration
    Ï• = current_state(ls)
    buf = buffers(ls)
    integrator = time_integrator(ls)
    # dynamic dispatch. Should not be a problem provided enough computation is
    # done inside of the function below
    out = _integrate!(Ï•, buf, integrator, ls.terms, tc, tf, Î”t)
    ls.t = tf
    # a copy may be needed if the last buffer is not the state
    out === Ï• || copy!(values(Ï•), values(out))
    return ls
end

number_of_buffers(fe::ForwardEuler) = 1

@noinline function _integrate!(Ï•, buffers, integrator::ForwardEuler, terms, tc, tf, Î”t)
    buffer = buffers[1]
    Î± = cfl(integrator)
    Î”t_cfl = Î± * compute_cfl(terms, Ï•, tc)
    Î”t = min(Î”t, Î”t_cfl)
    while tc <= tf - eps(tc)
        Î”t = min(Î”t, tf - tc) # if needed, take a smaller time-step to exactly land on tf
        for I in eachindex(Ï•)
            buffer[I] = _compute_terms(terms, Ï•, I, tc)
            buffer[I] = Ï•[I] - Î”t * buffer[I] # muladd?
        end
        Ï•, buffer = buffer, Ï• # swap the roles, no copies
        tc += Î”t
        @debug tc, Î”t
    end
    # @assert tc â‰ˆ tf
    return Ï•
end

number_of_buffers(fe::RK2) = 2

function _integrate!(Ï•::LevelSet, buffers, integrator::RK2, terms, tc, tf, Î”t)
    # Heun's method
    Î± = cfl(integrator)
    buffer1, buffer2 = buffers[1], buffers[2]
    Î”t_cfl = Î± * compute_cfl(terms, Ï•, tc)
    Î”t = min(Î”t, Î”t_cfl)
    while tc <= tf - eps(tc)
        Î”t = min(Î”t, tf - tc) # if needed, take a smaller time-step to exactly land on tf
        for I in eachindex(Ï•)
            tmp = _compute_terms(terms, Ï•, I, tc)
            buffer1[I] = Ï•[I] - Î”t * tmp
            buffer2[I] = Ï•[I] - 0.5 * Î”t * tmp
        end
        for I in eachindex(Ï•)
            tmp = _compute_terms(terms, buffer1, I, tc + Î”t)
            buffer2[I] -= 0.5 * Î”t * tmp
        end
        Ï•, buffer1, buffer2 = buffer2, Ï•, buffer1 # swap the roles, no copies
        tc += Î”t
        @debug tc, Î”t
    end
    # @assert tc â‰ˆ tf
    return Ï•
end

number_of_buffers(fe::RK3) = 2

function _integrate!(Ï•::LevelSet, buffers, integrator::RK3, terms, tc, tf, Î”t)
    buffer1, buffer2 = buffers
    Î± = cfl(integrator)
    Î”t_cfl = Î± * compute_cfl(terms, Ï•, tc)
    Î”t = min(Î”t, Î”t_cfl)
    while tc <= tf - eps(tc)
        Î”t = min(Î”t, tf - tc) # if needed, take a smaller time-step to exactly land on tf
        for I in eachindex(Ï•)
            tmp = _compute_terms(terms, Ï•, I, tc)
            buffer1[I] = Ï•[I] - Î”t * tmp # Ï•(t + Î”t)
        end
        for I in eachindex(Ï•)
            tmp = _compute_terms(terms, buffer1, I, tc + Î”t)
            buffer2[I] = buffer1[I] - Î”t * tmp # Ï•(t + 2Î”t)
            buffer2[I] = 1 / 4 * buffer2[I] + 3 / 4 * Ï•[I] # Ï•(t + Î”t/2) = 3/4 Ï•(t) + 1/4 Ï•(t + 2Î”t)
        end
        for I in eachindex(Ï•)
            tmp = _compute_terms(terms, buffer2, I, tc + 3 / 2 * Î”t)
            buffer1[I] = buffer2[I] - Î”t * tmp # Ï•(t + 3/2 Î”t)
            Ï•[I] = 1 / 3 * Ï•[I] + 2 / 3 * buffer1[I] # Ï•(t + Î”t) = 1/2 Ï•(t) + 2/3 Ï•(t + 3/2 Î”t)
        end
        tc += Î”t
        @debug tc, Î”t
    end
    # @assert tc â‰ˆ tf
    return Ï•
end

function _compute_terms(terms, Ï•, I, t)
    return sum(terms) do term
        return _compute_term(term, Ï•, I, t)
    end
end

"""
    grad_norm(Ï•::LevelSet[, I])

Compute the norm of the gradient of Ï• at index `I`, i.e. `|âˆ‡Ï•|`, or for all grid points
if `I` is not provided.
"""
function grad_norm(Ï•::LevelSet)
    msg = """level-set must have boundary conditions to compute gradient. See
    `add_boundary_conditions`."""
    has_boundary_conditions(Ï•) || error(msg)
    idxs = eachindex(Ï•)
    return map(i -> _compute_âˆ‡_norm(sign(Ï•[i]), Ï•, i), idxs)
end
function grad_norm(eq::LevelSetEquation)
    return grad_norm(current_state(eq))
end
