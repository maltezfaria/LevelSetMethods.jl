"""
    abstract type LevelSetTerm

A typical term in a level-set evolution equation.
"""
abstract type LevelSetTerm end

function compute_cfl(terms, Ï•, t)
    minimum(terms) do term
        return _compute_cfl(term, Ï•, t)
    end
end

# generic method, loops over indices
function _compute_cfl(term::LevelSetTerm, Ï•, t)
    dt = Inf
    for I in eachindex(Ï•)
        cfl = _compute_cfl(term, Ï•, I, t)
        dt = min(dt, cfl)
    end
    return dt
end

struct AdvectionTerm{V,S<:SpatialScheme} <: LevelSetTerm
    velocity::V
    scheme::S
end
velocity(adv::AdvectionTerm) = adv.velocity
scheme(adv::AdvectionTerm) = adv.scheme

"""
    AdvectionTerm(ğ®, scheme = WENO5())

Advection term representing  `ğ® â‹… âˆ‡Ï•`. Available `scheme`s are `Upwind` and `WENO5`.
"""
AdvectionTerm(ğ®, scheme = WENO5()) = AdvectionTerm(ğ®, scheme)

Base.show(io::IO, t::AdvectionTerm) = print(io, "ğ® â‹… âˆ‡ Ï•")

@inline function _compute_term(term::AdvectionTerm{V}, Ï•, I, t) where {V}
    sch = scheme(term)
    N = dimension(Ï•)
    ğ® = if V <: MeshField
        velocity(term)[I]
    elseif V <: Function
        x = mesh(Ï•)[I]
        velocity(term)(x, t)
    else
        error("velocity field type $V not supported")
    end
    # for dimension dim, compute the upwind derivative and multiply by the
    # velocity
    sum(1:N) do dim
        v = ğ®[dim]
        if v > 0
            if sch === Upwind()
                return v * Dâ»(Ï•, I, dim)
            elseif sch === WENO5()
                return v * weno5â»(Ï•, I, dim)
            else
                error("scheme $sch not implemented")
            end
        else
            if sch === Upwind()
                return v * Dâº(Ï•, I, dim)
            elseif sch === WENO5()
                return v * weno5âº(Ï•, I, dim)
            else
                error("scheme $sch not implemented")
            end
        end
    end
end

function _compute_cfl(term::AdvectionTerm{V}, Ï•, I, t) where {V}
    # equation 3.10 of Osher and Fedkiw
    ğ® = if V <: MeshField
        velocity(term)[I]
    elseif V <: Function
        x = mesh(Ï•)[I]
        velocity(term)(x, t)
    end
    Î”x = meshsize(Ï•)
    return 1 / maximum(abs.(ğ®) ./ Î”x)
end

"""
    struct CurvatureTerm{V,M} <: LevelSetTerm

Level-set curvature term representing `bÎº|âˆ‡Ï•|`, where `Îº = âˆ‡ â‹… (âˆ‡Ï•/|âˆ‡Ï•|) ` is
the curvature.
"""
struct CurvatureTerm{V} <: LevelSetTerm
    b::V
end
coefficient(cterm::CurvatureTerm) = cterm.b

Base.show(io::IO, t::CurvatureTerm) = print(io, "b Îº|âˆ‡Ï•|")

function _compute_term(term::CurvatureTerm, Ï•, I, t)
    N = dimension(Ï•)
    Îº = curvature(Ï•, I)
    b = coefficient(term)
    bI = if b isa MeshField
        b[I]
    elseif b isa Function
        x = mesh(Ï•)[I]
        b(x, t)
    else
        error("curvature field type $b not supported")
    end
    # compute |âˆ‡Ï•|
    Ï•2 = sum(1:N) do dim
        return Dâ°(Ï•, I, dim)^2
    end
    # update
    return bI * Îº * sqrt(Ï•2)
end

function _compute_cfl(term::CurvatureTerm, Ï•, I, t)
    b = coefficient(term)
    bI = if b isa MeshField
        b[I]
    elseif b isa Function
        x = mesh(Ï•)[I]
        b(x, t)
    else
        error("curvature field type $b not supported")
    end
    Î”x = minimum(meshsize(Ï•))
    return (Î”x)^2 / (2 * abs(bI))
end

"""
    struct NormalMotionTerm{V,M} <: LevelSetTerm

Level-set advection term representing  `v |âˆ‡Ï•|`. This `LevelSetTerm` should be
used for internally generated velocity fields; for externally generated velocities you may
use `AdvectionTerm` instead.
"""
@kwdef struct NormalMotionTerm{V} <: LevelSetTerm
    speed::V
end
speed(adv::NormalMotionTerm) = adv.speed

Base.show(io::IO, t::NormalMotionTerm) = print(io, "v|âˆ‡Ï•|")

function _compute_term(term::NormalMotionTerm, Ï•, I, t)
    N = dimension(Ï•)
    u = speed(term)
    v = if u isa MeshField
        u[I]
    elseif u isa Function
        x = mesh(Ï•)[I]
        u(x, t)
    else
        error("velocity field type $u not supported")
    end
    âˆ‡âºÂ², âˆ‡â»Â² = sum(1:N) do dim
        # for first-order, dont use +- 0.5h ...
        h = meshsize(Ï•, dim)
        neg = Dâ»(Ï•, I, dim) + 0.5h * limiter(D2â»â»(Ï•, I, dim), D2â°(Ï•, I, dim))
        pos = Dâº(Ï•, I, dim) - 0.5h * limiter(D2âºâº(Ï•, I, dim), D2â°(Ï•, I, dim))
        return SVector(
            positive(neg) .^ 2 + negative(pos) .^ 2,
            negative(neg) .^ 2 + positive(pos) .^ 2,
        )
    end
    return positive(v) * sqrt(âˆ‡âºÂ²) + negative(v) * sqrt(âˆ‡â»Â²)
end

function _compute_cfl(term::NormalMotionTerm, Ï•, I, t)
    u = speed(term)
    v = if u isa MeshField
        u[I]
    elseif u isa Function
        x = mesh(Ï•)[I]
        u(x, t)
    else
        error("velocity field type $u not supported")
    end
    Î”x = minimum(meshsize(Ï•))
    return Î”x / abs(v)
end

@inline positive(x) = x > zero(x) ? x : zero(x)
@inline negative(x) = x < zero(x) ? x : zero(x)

# eq. (6.20-6.21)
function g(x, y)
    tmp = zero(x)
    if x > zero(x)
        tmp += x * x
    end
    if y < zero(x)
        tmp += y * y
    end
    return sqrt(tmp)
end

# eq. (6.28)
function limiter(x, y)
    x * y < zero(x) || return zero(x)
    return abs(x) <= abs(y) ? x : y
end

"""
    struct ReinitializationTerm <: LevelSetTerm

Level-set term representing  `sign(Ï•) (|âˆ‡Ï•| - 1)`. This `LevelSetTerm` should be
used for reinitializing the level set into a signed distance function: for a
sufficiently large number of time steps this term allows one to solve the
Eikonal equation |âˆ‡Ï•| = 1.
"""
@kwdef struct ReinitializationTerm <: LevelSetTerm end

Base.show(io::IO, t::ReinitializationTerm) = print(io, "sign(Ï•) (|âˆ‡Ï•| - 1)")

function _compute_term(term::ReinitializationTerm, Ï•, I, t)
    v = sign(Ï•[I])
    âˆ‡ = _compute_âˆ‡_normal_motion(v, Ï•, I)
    return (âˆ‡ - 1.0) * v
end

_compute_cfl(term::ReinitializationTerm, Ï•, I, t) = minimum(meshsize(Ï•))
